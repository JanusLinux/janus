# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=glib2
pkgver=2.58.2
pkgrel=1
pkgdesc="Low level core library"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="https://wiki.gnome.org/Projects/GLib"
license=('GPL')
depends=('musl' 'libz' 'util-linux-libs' 'pcre')
makedepends=('meson')
options=('!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
	'gio-querymodules.hook'
	'glib-compile-schemas.hook')
md5sums=('8483db194fd8a4653394a1413df97a4c'
         'a52ea506b5cf162a4f72d09cc33fae50'
         '9e25d7de10bc4dc105f171e9ae46ef69')

build() {
	cd "$srcdir"/glib-$pkgver
	mkdir -p build
	cd build

	meson "$srcdir"/glib-$pkgver \
		--prefix / \
		--libdir /lib \
		--libexecdir /lib \
		--bindir /bin \
		--sbindir /bin \
		--includedir /include \
		--datadir /share \
		--mandir /share/man \
		--infodir /share/info \
		--localedir /share/locale \
		--sysconfdir /etc \
		--localstatedir /var \
		--sharedstatedir /var/lib \
		--buildtype release \
		--auto-features enabled \
		--wrap-mode nofallback \
		-D b_lto=true \
		-Dman=false \
		-D gtk_doc=false \
		-Dselinux=false
	ninja
}

package() {
	cd "$srcdir"/glib-$pkgver/build
	DESTDIR="$pkgdir" ninja install

	install -Dt $pkgdir/share/libalpm/hooks -m644 $srcdir/*.hook
}

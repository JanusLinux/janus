# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=glib2
pkgver=2.58.3
pkgrel=1
pkgdesc="Low level core library"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://wiki.gnome.org/Projects/GLib"
license=('GPL')
depends=('musl' 'libz' 'util-linux' 'pcre')
makedepends=('meson')
options=('!emptydirs')
source=("http://ftp.gnome.org/pub/gnome/sources/glib/${pkgver%.*}/glib-$pkgver.tar.xz"
	'gio-querymodules.hook'
	'glib-compile-schemas.hook')
md5sums=('8058c7bde846dcffe5fa453eca366d73'
         'a52ea506b5cf162a4f72d09cc33fae50'
         '9e25d7de10bc4dc105f171e9ae46ef69')

build() {
	cd "$srcdir"/glib-$pkgver
	mkdir -p build
	cd build

	meson "$srcdir"/glib-$pkgver \
		--prefix / \
		--buildtype release \
		--auto-features enabled \
		--wrap-mode nofallback \
		-D b_lto=true \
		-Dman=false \
		-D gtk_doc=false \
		-Dselinux=false
	ninja
}

package() {
	cd "$srcdir"/glib-$pkgver/build
	DESTDIR="$pkgdir" ninja install

	install -Dt $pkgdir/share/libalpm/hooks -m644 $srcdir/*.hook
}

# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=filesystem
pkgver=20180601
pkgrel=1
pkgdesc="Filesystem layout and configuration files"
arch=('any')
url="https://januslinux.github.io/"
license=('MIT')
groups=('base')
source=('fstab'
	'group'
	'host.conf'
	'hostname'
	'hosts'
	'inittab'
	'interfaces'
	'issue'
	'motd'
	'passwd'	
	'profile'
	'protocols'
	'rc.init'
	'rc.local'
	'rc.shutdown'
	'resolv.conf'
	'securetty'
	'services'
	'shadow'
	'shells'
	'sysctl.conf'
	'runsvdir-start'
	'tty1'
	'tty2'
	'tty3'
	'tty4'
	'ttyS0'
	'sysklogd'
	'crond'
	'crond-log'
	'dmesg')
backup=('etc/fstab'
	'etc/group'
	'etc/host.conf'
	'etc/hostname'
	'etc/hosts'
	'etc/inittab'
	'etc/issue'
	'etc/motd'
	'etc/passwd'
	'etc/profile'
	'etc/protocols'
	'etc/rc.local'
	'etc/resolv.conf'
	'etc/securetty'
	'etc/services'
	'etc/shadow'
	'etc/shells'
	'etc/sysctl.conf'
	'etc/network/interfaces')

package() {
	cd $pkgdir

	for d in boot dev etc/{skel,service,profile.d} home mnt usr var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	ln -s /proc/self/mounts $pkgdir/etc/mtab

	for f in fstab group host.conf hostname hosts issue inittab motd passwd profile protocols resolv.conf securetty services shells sysctl.conf; do
		install -m644 $srcdir/$f $pkgdir/etc
	done

	for f in shadow; do
		install -m600 $srcdir/$f $pkgdir/etc
	done

	mkdir -p \
		$pkgdir/etc/network/if-down.d \
		$pkgdir/etc/network/if-post-down.d \
		$pkgdir/etc/network/if-post-up.d \
		$pkgdir/etc/network/if-pre-down.d \
		$pkgdir/etc/network/if-pre-up.d \
		$pkgdir/etc/network/if-up.d

	cp $srcdir/interfaces $pkgdir/etc/network/interfaces

	install -Dm0755 $srcdir/rc.local $pkgdir/etc/rc.local

	for d in cache local opt log/{old,dmesg,crond} lib/misc empty service spool/cron/crontabs; do
		install -d -m755 var/$d
	done

	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	for d in bin include lib share/misc src; do
		install -d -m755 usr/$d
	done

	ln -s usr/lib lib

	ln -s usr/bin bin
	ln -s usr/bin sbin
	ln -s bin usr/sbin

	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/$d
	done

	for f in rc.init rc.shutdown; do
		install -Dm0755 $srcdir/$f $pkgdir/usr/bin/$f
	done

	install -Dm0755 $srcdir/runsvdir-start $pkgdir/usr/bin/runsvdir-start

	for ttys in tty1 tty2 tty3 tty4 ttyS0; do
		install -Dm0755 $srcdir/$ttys $pkgdir/etc/service/$ttys/run
		cd $pkgdir
		ln -sf /etc/service/$ttys var/service/$ttys
	done

	install -Dm0755 $srcdir/crond $pkgdir/etc/service/cron/run
	install -Dm0755 $srcdir/crond-log $pkgdir/etc/service/cron/log/run

	for f in sysklogd dmesg; do
		install -Dm0755 $srcdir/$f $pkgdir/etc/service/$f/run
		cd $pkgdir
		ln -sf /etc/service/$f var/service/$f
	done
}

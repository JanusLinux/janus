# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=filesystem
pkgver=20180425
pkgrel=1
pkgdesc="Filesystem layout and configuration files"
arch=('any')
url="https://januslinux.github.io/"
license=('MIT')
groups=('base')
source=('fstab'
	'group'
	'gshadow'
	'host.conf'
	'hostname'
	'hosts'
	'inittab'
	'issue'
	'motd'
	'passwd'	
	'profile'
	'protocols'
	'rc.init'
	'rc.local'
	'rc.shutdown'
	'resolv.conf'
	'securetty'
	'services'
	'shadow'
	'shells')
backup=('etc/fstab'
	'etc/group'
	'etc/gshadow'
	'etc/host.conf'
	'etc/hostname'
	'etc/hosts'
	'etc/inittab'
	'etc/issue'
	'etc/motd'
	'etc/passwd'
	'etc/profile'
	'etc/protocols'
	'etc/rc.local'
	'etc/resolv.conf'
	'etc/securetty'
	'etc/services'
	'etc/shadow'
	'etc/shells')

package() {
	cd $pkgdir

	for d in boot dev etc home mnt usr var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	ln -s /proc/self/mounts $pkgdir/etc/mtab

	for f in fstab group host.conf hostname hosts issue inittab motd passwd profile protocols resolv.conf securetty services shells; do
		install -m644 $srcdir/$f $pkgdir/etc
	done

	for f in shadow gshadow; do
		install -m600 $srcdir/$f $pkgdir/etc
	done

	install -Dm0755 $srcdir/rc.local $pkgdir/etc/rc.local

	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done

	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games
	ln -s spool/mail var/mail
	ln -s ../run var/run
	ln -s ../run/lock var/lock

	for d in bin include lib share/misc src; do
		install -d -m755 usr/$d
	done

	ln -s usr/lib lib

	ln -s usr/bin bin
	ln -s usr/bin sbin
	ln -s bin usr/sbin

	for d in bin etc games include lib man sbin share src; do
		install -d -m755 usr/local/$d
	done

	for f in rc.init rc.shutdown; do
		install -Dm0755 $srcdir/$f $pkgdir/usr/bin/$f
	done
}

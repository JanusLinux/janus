# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=shadow
pkgver=4.5
pkgrel=1
pkgdesc="Password and account management tool suite"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="https://github.com/shadow-maint/shadow"
license=('BSD')
groups=('base')
depends=('musl' 'acl')
backup=('etc/login.defs'
	'etc/default/useradd')
options=('debug')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sbindir=/usr/bin \
		--libdir=/usr/lib \
		--sysconfdir=/etc \
		--build=$XHOST \
		--host=$XTARGET \
		--with-group-max-length=32 \
		--without-audit \
		--without-libcrack \
		--without-libpam \
		--without-nscd \
		--without-selinux \
		--disable-nls
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install
	sed -i 's/1000/100/' $pkgdir/etc/default/useradd

	mv $pkgdir/usr/sbin/* $pkgdir/usr/bin
	rmdir $pkgdir/usr/sbin

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,misc,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

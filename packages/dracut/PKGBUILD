# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=dracut
pkgver=049
pkgrel=1
pkgdesc="Generic, modular, cross-distribution initramfs generation tool"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://dracut.wiki.kernel.org/"
license=('GPL')
depends=('musl-fts' 'util-linux' 'bash' 'kbd' 'kmod' 'libarchive')
makedepends=('docbook-xsl' 'asciidoc')
optdepends=('cryptsetup: Part of the Crypto setup'
	'dmraid: Part of the Raid setup'
	'lvm2: Part of the LVM setup'
	'mdadm: Part of the MDadmin thing'
	'kexec-tools: For switch kernels without reboot support'
	'gnupg>=2.1: For OpenPGP smartcards support')
backup=('etc/dracut.conf')
source=("https://github.com/dracutdevs/dracut/archive/$pkgver.tar.gz"
	'wordsize.patch')
md5sums=('614fc43ab6074fb7c48c0d38979dc9df'
         '6072cc99850668814e75dd8339912bf0')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -p0 -i "$srcdir"/wordsize.patch
	sed -i 's|$(arch)|$(uname -m)|g' modules.d/90crypt/module-setup.sh
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--sysconfdir=/etc \
		--bashcompletiondir=/share/bash-completion/completions
	make LIBS="-lfts"
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	rm -rf "$pkgdir"/lib/dracut/modules.d/*systemd*
	rm -rf "$pkgdir"/share/man/man8/*.service.*
	rm -rf "$pkgdir"/lib/kernel
}

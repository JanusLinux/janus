# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=libnl
pkgver=3.4.0
pkgrel=1
pkgdesc="Library for applications dealing with netlink sockets"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="https://github.com/thom311/libnl/"
license=('GPL')
depends=('musl')
backup=('etc/libnl/classid'
		'etc/libnl/pktloc')
source=("https://github.com/thom311/libnl/releases/download/libnl${pkgver//./_}/${pkgname}-${pkgver}.tar.gz"
		'musl.patch')
md5sums=('8f71910c03db363b41e2ea62057a4311'
         'bc872c3dfdc7e73c332adeb5034ccee2')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -Np1 -i "$srcdir"/musl.patch
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--sbindir=/bin \
		--sysconfdir=/etc \
		--disable-static \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

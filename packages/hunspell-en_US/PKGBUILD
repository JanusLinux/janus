# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=hunspell-en_US
pkgver=2018.04.16
pkgrel=1
pkgdesc="US English hunspell dictionaries"
arch=('any')
url="http://wordlist.aspell.net/dicts/"
license=('LGPL-2.0-or-later scowl')
depends=('hunspell')
provides=('hunspell-en')
replaces=('hunspell-en')
conflicts=('hunspell-en')
source=("http://downloads.sourceforge.net/project/wordlist/speller/$pkgver/hunspell-en_US-large-$pkgver.zip")
md5sums=('ee2dd9431ea78c252c8f7c64481cd60c')

prepare() {
	cd "$srcdir"
	sed -i "s:SET UTF8:SET UTF-8:g" *.aff
}

package() {
	cd "$srcdir"
	install -dm755 "$pkgdir"/share/hunspell
	install -m644 en_US*.dic en_US*.aff "$pkgdir"/share/hunspell

	pushd "$pkgdir"/share/hunspell/
		en_US_aliases="en_US"
		for lang in $en_US_aliases; do
			ln -sf en_US-large.aff $lang.aff
			ln -sf en_US-large.dic $lang.dic
		done
	popd

	install -dm755 "$pkgdir"/share/myspell/dicts
	pushd "$pkgdir"/share/myspell/dicts
		for file in "$pkgdir"/share/hunspell/*; do
			ln -sfv /share/hunspell/$(basename $file) .
		done
	popd
}

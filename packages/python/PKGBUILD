# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=python
pkgver=3.7.2
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="A high-level scripting language"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="http://www.python.org/"
license=('custom')
depends=('musl' 'libz' 'libedit'  'sqlite' 'bzip2' 'gdbm' 'openssl' 'expat' 'libffi')
makedepends=('tcl')
provides=('python3')
replaces=('python3')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-$pkgver.tar.xz")
md5sums=('df6ec36011808205beda239c72f947cb')

prepare() {
	cd "$srcdir"/Python-$pkgver
	sed -i -e "s|testCongestion|disabled_&|" Lib/test/test_socket.py
	sed -i -e "s|^#.* /usr/local/bin/python|#!/bin/python|" Lib/cgi.py
	sed -i -e "s|-flto |-flto=4 |g" configure configure.ac

	rm -r Modules/expat \
		Modules/zlib \
		Modules/_ctypes/darwin* \
		Modules/_ctypes/libffi*
}

build() {
	cd "$srcdir"/Python-$pkgver
	./configure \
		--prefix='' \
		--with-computed-gotos \
		--with-dbmliborder=gdbm:ndbm \
		--with-lto \
		--with-system-expat \
		--with-system-ffi \
		--with-threads \
		--enable-ipv6 \
		--enable-loadable-sqlite-extensions \
		--enable-shared \
		--disable-rpath \
		--build=$CHOST
	make EXTRA_CFLAGS="$CFLAGS -DTHREAD_STACK_SIZE=0x100000"
}

package() {
	cd "$srcdir"/Python-$pkgver
	sed -i 's/^all:.*$/all: build_all/' Makefile

	make DESTDIR="$pkgdir" install

	ln -sf python3 "$pkgdir"/bin/python
	ln -sf python3-config "$pkgdir"/bin/python-config
	ln -sf idle3 "$pkgdir"/bin/idle
	ln -sf pydoc3 "$pkgdir"/bin/pydoc

	install -dm755 "$pkgdir"/lib/python${_pybasever}/Tools/{i18n,scripts}
	install -m755 Tools/i18n/{msgfmt,pygettext}.py "$pkgdir"/lib/python${_pybasever}/Tools/i18n/
	install -m755 Tools/scripts/{README,*py} "$pkgdir"/lib/python${_pybasever}/Tools/scripts/
}

# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=python
pkgver=3.7.0
pkgrel=1
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64' 'aarch64' 'armhf')
url="http://www.python.org/"
license=('custom')
groups=('base')
depends=('musl' 'zlib' 'readline' 'expat' 'libffi' 'libressl')
source=("https://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.xz"
	"musl-find_library.patch"
	"fix-xattrs-glibc.patch"
	"bpo-30353.patch"
	"libressl.patch"
	"libressl-2.7.patch")

prepare() {
	cd "$srcdir/Python-$pkgver"
	patch -Np1 -i $srcdir/musl-find_library.patch
	patch -Np1 -i $srcdir/fix-xattrs-glibc.patch
	patch -Rp1 < $srcdir/bpo-30353.patch
	patch -Np1 -i $srcdir/libressl.patch
	patch -Np1 -i $srcdir/libressl-2.7.patch

cat > config.cache << EOF
ac_cv_have_long_long_format=yes
ac_cv_file__dev_ptmx=yes
ac_cv_file__dev_ptc=yes
ac_cv_working_tzset=yes
ac_cv_prog_HAS_HG=/bin/false
EOF

	rm -r Modules/expat \
		Modules/zlib \
		Modules/_ctypes/darwin* \
		Modules/_ctypes/libffi*
}

build() {
	cd "$srcdir/Python-$pkgver"
	OPT="" \
	./configure \
		--prefix=/usr \
		--build=$XHOST \
		--host=$XTARGET \
		--cache-file=config.cache \
		--with-ensurepip=no \
		--with-lto \
		--with-system-expat \
		--with-system-ffi \
		--with-threads \
		--enable-ipv6 \
		--enable-shared \
		--disable-rpath
	make EXTRA_CFLAGS="$CFLAGS -DTHREAD_STACK_SIZE=0x100000"
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/Python-$pkgver"
	make -j1 EXTRA_CFLAGS="$CFLAGS" DESTDIR=$pkgdir install

	chmod 755 $pkgdir/usr/lib/libpython3.6m.so
	chmod 755 $pkgdir/usr/lib/libpython3.so

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

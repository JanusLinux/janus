# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=gdb
pkgver=8.1
pkgrel=2
pkgdesc='The GNU Debugger'
arch=('x86_64' 'aarch64' 'armhf')
url='http://www.gnu.org/software/gdb/'
license=('GPL')
groups=('base')
depends=('musl' 'zlib' 'ncurses' 'readline')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
	./configure \
		--prefix=/usr \
		--build=$XHOST \
		--host=$XTARGET \
		--target=$XTARGET \
		--without-guile \
		--with-system-readline \
		--with-system-zlib \
		--disable-gdbserver \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	rm -f $pkgdir/usr/share/info/bfd.info
	rm -f $pkgdir/usr/share/info/dir

	rm -rf $pkgdir/usr/include
	rm -rf $pkgdir/usr/lib

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

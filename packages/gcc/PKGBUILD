# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=gcc
pkgver=7.3.0
pkgrel=1
pkgdesc="The GNU Compiler Collection - C and C++ frontends"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="http://gcc.gnu.org/"
license=('custom' 'FDL' 'GPL' 'LGPL')
groups=('base')
depends=('musl' 'zlib' 'binutils' 'mpc')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver/$pkgname-$pkgver.tar.xz")
options=(!emptydirs)

prepare() {
	cd $srcdir/$pkgname-$pkgver
	sed -i 's@\./fixinc\.sh@-c true@' gcc/Makefile.in
	sed -i '/m64=/s/lib64/lib/' gcc/config/i386/t-linux64
	sed -i '/lp64=/s/lib64/lib/' gcc/config/aarch64/t-aarch64-linux
	mkdir build
}

build() {
	cd "$srcdir/$pkgname-$pkgver"

	export CFLAGS_FOR_TARGET="$CFLAGS"
	export CXXFLAGS_FOR_TARGET="$CXXFLAGS"
	export CPPFLAGS_FOR_TARGET="$CPPFLAGS"
	export LDFLAGS_FOR_TARGET="$LDFLAGS"

	export CFLAGS_FOR_BUILD="$CFLAGS"
	export CPPFLAGS_FOR_BUILD="$CPPFLAGS"
	export CXXFLAGS_FOR_BUILD="$CXXFLAGS"
	export LDFLAGS_FOR_BUILD="$LDFLAGS"

	cd build
	../configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--build=$XHOST \
		--host=$XTARGET \
		--target=$XTARGET \
		--with-system-zlib \
		--enable-__cxa_atexit \
		--enable-clocale=generic \
		--enable-default-pie \
		--enable-deterministic-archives \
		--enable-languages=c,c++ \
		--enable-libstdcxx-time \
		--enable-lto \
		--enable-threads=posix \
		--enable-tls \
		--disable-gnu-indirect-function \
		--disable-libmpx \
		--disable-libmudflap \
		--disable-libsanitizer \
		--disable-libstdcxx-pch \
		--disable-multilib \
		--disable-nls \
		--disable-symvers \
		$GCCOPTS
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	cd build
	make DESTDIR=$pkgdir install

	ln -s gcc $pkgdir/usr/bin/cc

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,misc,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

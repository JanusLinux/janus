# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=python3
pkgver=3.7.2
pkgrel=1
_pybasever=${pkgver%.*}
pkgdesc="A high-level scripting language"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64el' 'mipsel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.python.org/"
license=('custom')
depends=('musl' 'libz' 'libedit'  'sqlite' 'bzip2' 'gdbm' 'openssl' 'expat' 'libffi')
makedepends=('tcl')
source=("https://www.python.org/ftp/python/${pkgver%rc*}/Python-$pkgver.tar.xz")
md5sums=('df6ec36011808205beda239c72f947cb')

prepare() {
	cd "$srcdir"/Python-$pkgver
	sed -i -e "s|-flto |-flto=4 |g" configure configure.ac

	rm -r Modules/expat \
		Modules/_ctypes/darwin* \
		Modules/_ctypes/libffi*
}

build() {
	cd "$srcdir"/Python-$pkgver
	./configure \
		--prefix=/ \
		--with-computed-gotos \
		--with-dbmliborder=gdbm:ndbm \
		--with-ensurepip=yes \
		--with-lto \
		--with-system-expat \
		--with-system-ffi \
		--with-threads \
		--enable-ipv6 \
		--enable-loadable-sqlite-extensions \
		--enable-shared \
		--disable-rpath \
		--build=$CHOST
	make EXTRA_CFLAGS="$CFLAGS -DTHREAD_STACK_SIZE=0x100000"
}

package() {
	cd "$srcdir"/Python-$pkgver
	make DESTDIR="$pkgdir" install

	chmod 755 "$pkgdir"/lib/libpython3.7m.so
	chmod 755 "$pkgdir"/lib/libpython3.so
}

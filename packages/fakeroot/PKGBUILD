# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=fakeroot
pkgver=1.22
pkgrel=1
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user"
arch=('x86_64' 'aarch64')
url="http://fakeroot.alioth.debian.org/"
license=('GPL')
groups=('base')
depends=('musl' 'attr' 'acl' 'libcap')
source=("http://ftp.debian.org/debian/pool/main/f/fakeroot/fakeroot_$pkgver.orig.tar.bz2"
	'busybox-getopt.patch'
	'fakeroot-hide-dlsym-errors.patch'
	'fakeroot-no64.patch'
	'fakeroot-stdint.patch'
	'fakeroot-no-ldlibrarypath.patch'
	'xstatjunk.patch')

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i $srcdir/busybox-getopt.patch
	patch -Np1 -i $srcdir/fakeroot-hide-dlsym-errors.patch
	patch -Np1 -i $srcdir/fakeroot-no64.patch
	patch -Np1 -i $srcdir/fakeroot-stdint.patch
	patch -Np1 -i $srcdir/fakeroot-no-ldlibrarypath.patch
	patch -Np1 -i $srcdir/xstatjunk.patch
	for d in de es fr nl pt sv ; do
		sed -i '/man_MANS = /s@=.*@=@' "doc/${d}/Makefile.am"
	done
	sed -i 's@puts.*);\]@\]@g' configure.ac
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	export CFLAGS="-D_STAT_VER=0 $CFLAGS"
	CONFIG_SHELL=/bin/sh ./bootstrap
	CONFIG_SHELL=/bin/sh ./configure \
		--prefix=/usr \
		--build=$XHOST \
		--host=$XTARGET
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

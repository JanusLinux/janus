# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=ruby
pkgver=2.6.0
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.ruby-lang.org/en/"
license=('Ruby BSD-2-Clause')
depends=('musl' 'libz' 'gmp' 'gdbm' 'db' 'readline' 'openssl' 'libffi' 'yaml')
options=('!emptydirs')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver:0:3}/ruby-${pkgver}.tar.xz"
	'rubygems-avoid-platform-specific-gems.patch')
md5sums=('0b77dc26b379a0088be45488e0eda60c'
         'd7224759494b53ba032a654dad348bad')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	patch -Np1 -i "$srcdir"/rubygems-avoid-platform-specific-gems.patch
}

build() {
	export CFLAGS="${CFLAGS/-fomit-frame-pointer}"
	export CXXFLAGS="${CXXFLAGS/-fomit-frame-pointer}"
	export CFLAGS="$CFLAGS -fno-omit-frame-pointer -fno-strict-aliasing"
	export CPPFLAGS="$CPPFLAGS -fno-omit-frame-pointer -fno-strict-aliasing"

	export INSTALL=install

	export ac_cv_func_isnan=yes
	export ac_cv_func_isinf=yes

	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--libexecdir=/lib/ruby \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--sharedstatedir=/var/lib \
		--with-dbm-type=gdbm_compat \
		--enable-pthread \
		--enable-shared \
		--disable-install-doc \
		--disable-install-rdoc \
		--disable-install-capi \
		--disable-rpath \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

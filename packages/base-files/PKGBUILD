# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=base-files
pkgver=0.1
pkgrel=1
pkgdesc="Filesystem layout and basic configuration files"
arch=('any')
url="https://projectblaze.github.io/"
license=('MIT')
options=('docs')
install=$pkgname.install
backup=('etc/crypttab'
	'etc/fstab'
	'etc/group'
	'etc/gshadow'
	'etc/host.conf'
	'etc/hosts'
	'etc/issue'
	'etc/motd'
	'etc/passwd'
	'etc/profile'
	'etc/protocols'
	'etc/resolv.conf'
	'etc/securetty'
	'etc/services'
	'etc/shadow'
	'etc/shells'
	'etc/modprobe.d/10-blacklist.conf')
source=('blacklist.conf'
	'crypttab'
	'enter-chroot'
	'fstab'
	'genfstab'
	'group'
	'gshadow'
	'host.conf'
	'hosts'
	'issue'
	'motd'
	'os-release'
	'pacstrap'
	'passwd'
	'profile'
	'protocols'
	'resolv.conf'
	'securetty'
	'services'
	'shadow'
	'shells'
	'sysctl'
	'zzz')
md5sums=('a8ff6379f3fc9437c57788f1d0e54945'
         '3c15dd9a5cf5fe5d04fb1cbd5c1662b8'
         '9b7720d94d6e396a68076da60f34a438'
         'e33f6dfdd61978fcb3ddf1431286e05a'
         '2dde31048862f75aff6cffff91d5470f'
         'e9a2c0d52e1c6983a5592a43a7bdf4ea'
         'b3991ca61b86745c1e5691c69f183ad9'
         'a61b9f6548d337c1cc1e5a4de39f7b7f'
         '0166671b0bfc53018c8d7a6ec53dfcb0'
         'a8509f6b5c267ba927bbeedb79a7c793'
         'd41d8cd98f00b204e9800998ecf8427e'
         'd3c8ab2fc3ad97aa6bf9eb0dec990255'
         '7f87ab9fb1bf8665218ed7817e43462f'
         '41c165dc4499742152e80a8fdbd63db9'
         'f35b6d1f3192017cf3ef1c71ff6a9e70'
         'd46c8af3927f3c5f748e359871bdf236'
         'fe0b9046ae1a0ee676446c08ef4f2af7'
         '49c1d411ff33b6df4d55a6a6f9a9bf92'
         '9d39e51e6bd5a30168c26c57b66d2e37'
         '2ef2429c2c98d0883906aa6f5363be80'
         'da19d7887faad35385c2cc5672e294ea'
         '3678409d55c834d709ffe356a6b3cfc5'
         'b6bfe108600d3985dd2d8ba0090fe5f7')

package() {
	cd "$pkgdir"
	for d in bin include lib/{modules,firmware} share/misc boot dev etc/{skel,profile.d,modprobe.d,sysctl.d} home mnt var opt srv/http run; do
		install -d -m755 $d
	done

	install -d -m555 proc
	install -d -m555 sys
	install -d -m0750 root
	install -d -m1777 tmp
	install -d -m555 -g 11 srv/ftp

	ln -sf ../proc/self/mounts etc/mtab

	touch etc/blaze-release

	for f in fstab group host.conf hosts issue motd passwd profile protocols resolv.conf securetty services shells; do
		install -m644 "$srcdir"/$f etc/
	done

	for f in gshadow shadow crypttab; do
		install -m600 "$srcdir"/$f etc/
	done

	for d in cache local opt log/old lib/misc empty; do
		install -d -m755 var/$d
	done

	install -d -m1777 var/{tmp,spool/mail}

	install -d -m775 -g 50 var/games

	ln -sf spool/mail var/mail
	ln -sf ../run var/run
	ln -sf ../run/lock var/lock

	for d in {1..8}; do
		install -d -m755 share/man/man$d
	done


	for d in bin etc games include lib man sbin share src; do
		install -d -m755 local/$d
	done

	ln -sf ../man local/share/man

	install -m644 "$srcdir"/blacklist.conf "$pkgdir"/etc/modprobe.d/10-blacklist.conf
	install -D -m644 "$srcdir"/sysctl "$pkgdir"/etc/sysctl.d/10-default.conf

	install -D -m755 "$srcdir"/zzz "$pkgdir"/bin/zzz
	install -D -m755 "$srcdir"/enter-chroot "$pkgdir"/bin/enter-chroot
	install -D -m755 "$srcdir"/genfstab "$pkgdir"/bin/genfstab
	install -D -m755 "$srcdir"/pacstrap "$pkgdir"/bin/pacstrap

	ln -sfn . usr
	ln -sfn bin sbin
}

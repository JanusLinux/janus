# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=pciutils
pkgver=3.6.2
pkgrel=1
pkgdesc="PCI bus configuration space access library and tools"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://mj.ucw.cz/sw/pciutils/"
license=('GPL')
depends=('musl' 'kmod' 'hwids')
source=("ftp://atrey.karlin.mff.cuni.cz/pub/linux/pci/$pkgname-$pkgver.tar.gz")
md5sums=('a1c5ec13f81a1c9cf380c93623959005')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	make OPT="$CFLAGS -fPIC -DPIC" ZLIB=no SHARED=no PREFIX='' SHAREDIR=/share/hwdata MANDIR=/share/man SBINDIR=/bin lib/libpci.a 
	cp lib/libpci.a "$srcdir/"
	make clean
	make OPT="$CFLAGS" ZLIB=no SHARED=yes PREFIX='' SBINDIR=/bin SHAREDIR=/share/hwdata MANDIR=/share/man all
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make SHARED=yes PREFIX='' SBINDIR=/bin SHAREDIR=/share/hwdata MANDIR=/share/man DESTDIR="$pkgdir" install install-lib

	cp "$srcdir"/libpci.a "$pkgdir"/lib

	rm -rf "$pkgdir"/{{,s}bin/update-pciids,share/{man/man8/update-pciids.8,hwdata}}
}

# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=kmod
pkgver=25
pkgrel=5
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL-2.0-or-later')
depends=('musl' 'libz' 'xz' 'openrc')
options=('strip' 'debug')
install=$pkgname.install
source=("https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz"
	'depmod-search.conf'
	'kmod-static-nodes')
md5sums=('34f325cab568f842fdde4f8b2182f220'
         'dd9f04879dc9703338f40395e3d56f80'
         '0bfa63c6b5c7074eae04a2d09eb6d039')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--sysconfdir=/etc \
		--with-xz \
		--with-zlib \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	for tool in {ins,ls,rm,dep}mod mod{probe,info}; do
		ln -sf kmod "$pkgdir"/bin/$tool
	done

	install -dm755 "$pkgdir"/{etc,lib}/{depmod,modprobe}.d

	install -Dm644 "$srcdir"/depmod-search.conf "$pkgdir"/lib/depmod.d/search.conf

	install -Dm755 "$srcdir"/kmod-static-nodes "$pkgdir"/etc/init.d/kmod-static-nodes
}

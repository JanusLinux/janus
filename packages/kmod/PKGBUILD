# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=kmod
pkgver=25
pkgrel=1
pkgdesc="Linux kernel module management tools and library"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="http://git.kernel.org/?p=utils/kernel/kmod/kmod.git;a=summary"
license=('GPL')
groups=('base')
depends=('musl' 'zlib' 'xz')
source=("https://www.kernel.org/pub/linux/utils/kernel/$pkgname/$pkgname-$pkgver.tar.xz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--build=$XHOST \
		--host=$XTARGET \
		--with-xz \
		--with-zlib \
		--disable-manpages
	sed -i "s,/usr/include,$ROOTFS/usr/include,g" Makefile
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	for tool in depmod insmod modinfo modprobe rmmod; do
		ln -s kmod $pkgdir/usr/bin/$tool
	done

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

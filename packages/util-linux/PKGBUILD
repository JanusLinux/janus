# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=util-linux
pkgmajor=2.33
pkgver=$pkgmajor.1
pkgrel=1
pkgdesc="Miscellaneous system utilities for Linux"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="http://git.kernel.org/cgit/utils/util-linux/util-linux.git"
license=('GPL-2.0 GPL-2.0-or-later LGPL-2.0-or-later BSD Public-Domain')
depends=('musl' 'libz' 'libedit' 'libcap-ng')
options=('strip' 'debug' 'staticlibs')
source=("https://www.kernel.org/pub/linux/utils/util-linux/v$pkgmajor/$pkgname-$pkgver.tar.xz")
md5sums=('6fcfea2043b5ac188fd3eed56aeb5d90')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		ADJTIME_PATH=/var/lib/hwclock/adjtime \
		--prefix='' \
		--libdir=/lib \
		--bindir=/bin \
		--localstatedir=/var \
		--enable-fs-paths-extra=/bin \
		--with-btrfs \
		--with-python=3 \
		--with-readline \
		--without-systemd \
		--without-systemdsystemunitdir \
		--enable-kill \
		--enable-mesg \
		--enable-partx \
		--enable-raw \
		--enable-vipw \
		--enable-wall \
		--enable-write \
		--disable-login \
		--disable-nologin \
		--disable-su \
		--disable-sulogin \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	cd "$pkgdir"
	mv sbin/* bin/
	rm -rf sbin

	mkdir -p var/lib/hwclock
}

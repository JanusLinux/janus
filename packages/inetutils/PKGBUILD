# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=inetutils
pkgver=1.9.4
pkgrel=1
pkgdesc="A collection of common network programs"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://www.gnu.org/software/inetutils/"
license=('GPL-3.0+')
depends=('musl' 'ncurses' 'libcap')
options=('!emptydirs')
source=("http://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")
md5sums=('87fef1fa3f603aef11c41dcc097af75e')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--libexec=/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--enable-ncurses \
		--enable-servers \
		--disable-logger \
		--disable-rcp \
		--disable-rexec \
		--disable-rlogin \
		--disable-rsh \
		--disable-syslogd \
		--disable-whois \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

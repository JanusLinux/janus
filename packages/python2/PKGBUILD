# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=python2
pkgver=2.7.15
pkgrel=1
pkgdesc="Next generation of the python high-level scripting language"
arch=('x86_64' 'aarch64')
url="http://www.python.org/"
license=('custom')
groups=('base')
depends=('musl' 'zlib' 'readline' 'expat' 'libffi' 'libressl')
source=("https://www.python.org/ftp/python/$pkgver/Python-$pkgver.tar.xz"
	"musl-find_library.patch"
	"unchecked-ioctl.patch")

prepare() {
	cd "$srcdir/Python-$pkgver"
	patch -Np1 -i $srcdir/musl-find_library.patch
	patch -Np1 -i $srcdir/unchecked-ioctl.patch

	rm -r Modules/expat Modules/_ctypes/libffi* Modules/zlib
}

build() {
	cd "$srcdir/Python-$pkgver"
	export OPT="$CFLAGS -DTHREAD_STACK_SIZE=0x100000"
	./configure \
		--prefix=/usr \
		--build=$XHOST \
		--host=$XTARGET \
		--with-system-expat \
		--with-system-ffi \
		--with-system-zlib \
		--with-threads \
		--enable-ipv6 \
		--enable-shared \
		--enable-unicode=ucs4
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/Python-$pkgver"
	make -j1 DESTDIR=$pkgdir install

	chmod 755 $pkgdir/usr/lib/libpython2.7.so.1.0

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

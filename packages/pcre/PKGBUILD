# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=pcre
pkgver=8.42
pkgrel=1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="http://www.pcre.org/"
license=('BSD-3-Clause')
depends=('gcc-runtime' 'libz' 'libedit' 'bzip2' 'bash')
source=("https://ftp.pcre.org/pub/pcre/$pkgname-$pkgver.tar.bz2")
md5sums=('085b6aa253e0f91cae70b3cdbe8c1ac2')

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--enable-jit \
		--enable-pcre16 \
		--enable-pcre32 \
		--enable-pcregrep-libbz2 \
		--enable-pcregrep-libz \
		--enable-pcretest-libreadline \
		--enable-unicode-properties \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}

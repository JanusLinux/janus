# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=libedit
_pkgver=20181209-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc="Command line editor library providing generic line editing, history, and tokenization functions"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="http://thrysoee.dk/editline/"
license=('BSD-3-Clause')
depends=('musl' 'netbsd-curses')
source=("http://thrysoee.dk/editline/$pkgname-${_pkgver}.tar.gz")
md5sums=('22e945a0476e388e6f78bfc8d6e1192c')

build() {
	cd "$srcdir"/$pkgname-${_pkgver}
	./configure \
		--prefix='' \
		--enable-widec \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-${_pkgver}
	make DESTDIR="$pkgdir" install

	ln -sf libedit.a "$pkgdir"/lib/libreadline.a
	ln -sf libedit.so "$pkgdir"/lib/libreadline.so
	mkdir -p "$pkgdir"/include/readline
	touch "$pkgdir"/include/readline/history.h
	touch "$pkgdir"/include/readline/tilde.h
	ln -sf ../editline/readline.h "$pkgdir"/include/readline/readline.h
}

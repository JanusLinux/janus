# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=bzip2
pkgver=1.0.6
pkgrel=1
pkgdesc="A high-quality data compression program"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64el' 'mipsel' 'ppc64le' 'ppc64' 'ppc')
url="http://sources.redhat.com/bzip2"
license=('BSD')
depends=('musl' 'bash')
source=("https://sources.archlinux.org/other/packages/$pkgname/$pkgname-$pkgver.tar.gz"
	'bzip2-1.0.4-bzip2recover.patch'
	'bzip2-1.0.6-CVE-2016-3189.patch')
md5sums=('00b516f4704d4a7cb50a1d97e6e8e15b'
         'a9aee693365c1acfccff5592aa19d527'
         '9b2dab91fc059ea8f90f25afaa30c04b')

prepare() {
	cd "$srcdir"/bzip2-$pkgver
	sed -e 's/^CFLAGS=\(.*\)$/CFLAGS=\1 \$(BIGFILES)/' -i ./Makefile-libbz2_so
	sed -i "s|-O2|${CFLAGS} -fPIC|g" Makefile
	sed -i "s|-O2|${CFLAGS} -fPIC|g" Makefile-libbz2_so
	sed -i 's@\(ln -s -f \)$(PREFIX)/bin/@\1@' Makefile
	sed -i "s@(PREFIX)/man@(PREFIX)/share/man@g" Makefile
	patch -Np1 < "$srcdir"/bzip2-1.0.4-bzip2recover.patch
	patch -Np1 -i "$srcdir"/bzip2-1.0.6-CVE-2016-3189.patch
}


build() {
	cd "$srcdir"/$pkgname-$pkgver
	make
	make -f Makefile-libbz2_so
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make PREFIX="$pkgdir" install

	install -m755 libbz2.so.1.0.6 "$pkgdir"/lib
	ln -sf libbz2.so.1.0.6 "$pkgdir"/lib/libbz2.so
	ln -sf libbz2.so.1.0.6 "$pkgdir"/lib/libbz2.so.1
	ln -sf libbz2.so.1.0.6 "$pkgdir"/lib/libbz2.so.1.0
}

# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=bzip2
pkgver=1.0.6
pkgrel=1
pkgdesc="A high-quality data compression program"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="http://sources.redhat.com/bzip2"
license=('custom')
groups=('base')
depends=('musl')
source=("http://www.bzip.org/$pkgver/bzip2-$pkgver.tar.gz"
	'bzip2.patch')

build() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i $srcdir/bzip2.patch
	sed -i 's@all: libbz2.a bzip2 bzip2recover test@all: libbz2.a bzip2 bzip2recover@' Makefile
	make CFLAGS="$CFLAGS -D_GNU_SOURCE -fPIC" LDFLAGS="$LDFLAGS" CC="$CC"
	make CFLAGS="$CFLAGS -D_GNU_SOURCE -fPIC" LDFLAGS="$LDFLAGS" CC="$CC" -f Makefile-libbz2_so
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make PREFIX=$pkgdir/usr install
	make -f Makefile-libbz2_so PREFIX=$pkgdir/usr install

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,misc,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

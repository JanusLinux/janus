# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=zsh
pkgver=5.6.2
pkgrel=1
pkgdesc="A very advanced and programmable command interpreter (shell) for UNIX"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'mips64' 'mips' 'ppc64le' 'ppc64' 'ppc')
url="http://www.zsh.org/"
license=('BSD')
depends=('musl' 'netbsd-curses' 'libcap' 'pcre' 'gdbm')
backup=('etc/zsh/zprofile')
install=$pkgname.install
source=("https://download.sourceforge.net/project/$pkgname/$pkgname/$pkgver/$pkgname-$pkgver.tar.xz"
	'zprofile')
md5sums=('540673bf823d2e13806ac0395caa8345'
         '24a9335edf77252a7b5f52e079f7aef7')

prepare() {
	cd "$srcdir"/$pkgname-$pkgver
	sed -i 's#/usr/share/keymaps#/share/kbd/keymaps#g' Completion/Unix/Command/_loadkeys
	sed -i 's#/usr/share/misc/usb.ids#/share/hwdata/usb.ids#g' Completion/Linux/Command/_lsusb
	for _fpath in AIX BSD Cygwin Darwin Debian Mandriva openSUSE Redhat Solaris; do
		rm -rf Completion/$_fpath
		sed "s#\s*Completion/$_fpath/\*/\*##g" -i Src/Zle/complete.mdd
	done
	rm Completion/Linux/Command/_{pkgtool,rpmbuild}
}

build() {
	cd "$srcdir"/$pkgname-$pkgver
	./configure \
		--prefix='' \
		--with-tcsetpgrp \
		--enable-cap \
		--enable-etcdir=/etc/zsh \
		--enable-fndir=/share/zsh/functions \
		--enable-function-subdirs \
		--enable-maildir-support \
		--enable-multibyte \
		--enable-pcre \
		--enable-scriptdir=/share/zsh/scripts \
		--enable-zlogin=/etc/zsh/zlogin \
		--enable-zlogout=/etc/zsh/zlogout \
		--enable-zprofile=/etc/zsh/zprofile \
		--enable-zshenv=/etc/zsh/zshenv \
		--enable-zshrc=/etc/zsh/zshrc \
		--enable-zsh-secure-free \
		--build=$CHOST
	make
}

package() {
	cd "$srcdir"/$pkgname-$pkgver
	make DESTDIR="$pkgdir" install

	install -D -m644 "$srcdir"/zprofile "$pkgdir"/etc/zsh/zprofile
}

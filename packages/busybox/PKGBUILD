# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=busybox
pkgver=1.28.3
pkgrel=1
pkgdesc="Size optimized toolbox of many common UNIX utilities"
arch=('x86_64' 'aarch64')
url="https://www.busybox.net/"
license=('GPL')
groups=('base')
depends=('musl')
options=('debug')
source=("http://busybox.net/downloads/$pkgname-$pkgver.tar.bz2"
	'tty1'
	'tty2'
	'tty3'
	'tty4'
	'ttyS0'
	'crond'
	'crond-log'
	'dmesg')
install=$pkgname.install

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	make ARCH=$XKARCH CROSS_COMPILE=$XTARGET- defconfig
	sed -i 's/\(CONFIG_\)\(.*\)\(INETD\)\(.*\)=y/# \1\2\3\4 is not set/g' .config
	sed -i 's/\(CONFIG_IFPLUGD\)=y/# \1 is not set/' .config
	sed -i 's/\(CONFIG_FEATURE_WTMP\)=y/# \1 is not set/' .config
	sed -i 's/\(CONFIG_FEATURE_UTMP\)=y/# \1 is not set/' .config
	sed -i 's/\(CONFIG_UDPSVD\)=y/# \1 is not set/' .config
	sed -i 's/\(CONFIG_TCPSVD\)=y/# \1 is not set/' .config
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	make ARCH=$XKARCH CROSS_COMPILE=$XTARGET- EXTRA_CFLAGS="$CFLAGS"
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make ARCH=$XKARCH CROSS_COMPILE=$XTARGET- CONFIG_PREFIX=$pkgdir install

	cd $pkgdir

	for d in bin sbin usr/sbin; do
		mv $d/* usr/bin/
		rm -rf $d
	done

	chmod 4755 $pkgdir/usr/bin/busybox

	rm -rf linuxrc
	ln -s usr/bin/busybox init

	mkdir -p $pkgdir/usr/share/udhcpc
	install -Dm0755 $srcdir/$pkgname-$pkgver/examples/udhcp/simple.script $pkgdir/usr/share/udhcpc/default.script

	rm -rf $pkgdir/usr/bin/{blkdiscard,blkid,blockdev,cal,chattr,chrt,chvt,clear,deallocvt,dmesg,eject,fallocate,fdformat,fdisk,fgconsole,findfs,flock,fsck,fsck.minix,fsfreeze,fstrim,getopt,hexdump,hwclock,ionice,ipcrm,ipcs,kbd_mode,kill,less,linux*,logger,losetup,lsattr,lzcat,lzma,mesg,mke2fs,mkfs.ext2,mkfs.minix,mkswap,more,mount,mountpoint,nsenter,openvt,patch,pivot_root,readprofile,renice,reset,rev,rtcwake,script,scriptreplay,setarch,setfont,setkeycodes,setsid,sh,showkey,strings,swapoff,swapon,switch_root,taskset,umount,unlzma,unshare,unxz,xz,xzcat,depmod,insmod,modinfo,modprobe,rmmod,setfattr,bunzip2,bzcat,bzip2,dc}

	mkdir -p $pkgdir/etc/service $pkgdir/var/service

	for ttys in tty1 tty2 tty3 tty4 ttyS0; do
		install -Dm0755 $srcdir/$ttys $pkgdir/etc/service/$ttys/run
		cd $pkgdir
		ln -sf /etc/service/$ttys var/service/$ttys
	done

	install -Dm0755 $srcdir/crond $pkgdir/etc/service/crond/run
	install -Dm0755 $srcdir/crond-log $pkgdir/etc/service/crond/log/run

	for f in dmesg; do
		install -Dm0755 $srcdir/$f $pkgdir/etc/service/$f/run
		cd $pkgdir
		ln -sf /etc/service/$f var/service/$f
	done

	mkdir -p  $pkgdir/var/log/crond $pkgdir/var/log/dmesg $pkgdir/var/spool/cron/crontabs

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

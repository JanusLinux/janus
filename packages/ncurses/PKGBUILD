# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=ncurses
pkgver=6.1
pkgrel=1
pkgdesc="System V Release 4.0 curses emulation library"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="https://www.gnu.org/software/ncurses/"
license=('MIT')
groups=('base')
depends=('musl')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--build=$XHOST \
		--host=$XTARGET \
		--with-pkg-config-libdir=/usr/lib/pkgconfig \
		--with-normal \
		--with-shared \
		--without-ada \
		--without-debug \
		--without-manpages \
		--without-tests \
		--enable-pc-files \
		--enable-widec \
		--disable-nls
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	for lib in ncurses form panel menu; do
		echo "INPUT(-l${lib}w)" > "$pkgdir/usr/lib/lib${lib}.so"
		ln -s ${lib}w.pc "$pkgdir/usr/lib/pkgconfig/${lib}.pc"
	done

	for lib in tic tinfo; do
		echo "INPUT(libncursesw.so.${pkgver:0:1})" > "$pkgdir/usr/lib/lib${lib}.so"
		ln -s libncursesw.so.${pkgver:0:1} "$pkgdir/usr/lib/lib${lib}.so.${pkgver:0:1}"
	done

	echo 'INPUT(-lncursesw)' > "$pkgdir/usr/lib/libcursesw.so"
	ln -s libncurses.so "$pkgdir/usr/lib/libcurses.so"

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

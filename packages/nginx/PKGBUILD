# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=nginx
pkgver=1.15.0
pkgrel=2
pkgdesc='Lightweight HTTP server and IMAP/POP3 proxy server'
arch=('x86_64' 'aarch64')
url='https://nginx.org'
license=('custom')
groups=('base')
depends=('musl' 'zlib' 'libressl' 'pcre')
source=("http://nginx.org/download/nginx-$pkgver.tar.gz"
	'99-default'
	'nginx.conf'
	'nginx'
	'nginx-log')
backup=(etc/nginx/fastcgi.conf
        etc/nginx/fastcgi_params
        etc/nginx/koi-win
        etc/nginx/koi-utf
        etc/nginx/nginx.conf
        etc/nginx/scgi_params
        etc/nginx/uwsgi_params
        etc/nginx/win-utf)
install=$pkgname.install

build() {
	cd "$srcdir/$pkgname-$pkgver"
	unset CC CXX AR AS RANLIB LD STRIP
	./configure --with-cc-opt="-I$ROOTFS/usr/include" \
		--prefix=/usr \
		--sbin-path=/usr/bin/nginx \
		--conf-path=/etc/nginx/nginx.conf \
		--error-log-path=/var/log/nginx/error.log \
		--http-client-body-temp-path=/var/spool/nginx/body \
		--http-fastcgi-temp-path=/var/spool/nginx/fastcgi \
		--http-log-path=/var/log/nginx/access.log \
		--http-proxy-temp-path=/var/spool/nginx/proxy \
		--http-scgi-temp-path=/var/spool/nginx/scgi \
		--http-uwsgi-temp-path=/var/spool/nginx/uwsgi \
		--lock-path=/var/lock/nginx.lock \
		--pid-path=/var/run/nginx.pid \
		--user=nginx \
		--group=nogroup \
		--with-ipv6 \
		--with-threads \
		--with-http_ssl_module
	make CC="$XTARGET-gcc" CXX="$XTARGET-g++" AR="$XTARGET-ar" AS="$XTARGET-as" RANLIB="$XTARGET-ranlib" LD="$XTARGET-ld" STRIP="$XTARGET-strip"
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	install -d $pkgdir/usr/share/nginx
	mv $pkgdir/usr/html $pkgdir/usr/share/nginx/

	install -d $pkgdir/etc/nginx/conf.d
	install -d $pkgdir/etc/nginx/sites-available
	install -d $pkgdir/etc/nginx/sites-enabled
	install -m 0644 $srcdir/99-default $pkgdir/etc/nginx/sites-available/
	install -m 0644 $srcdir/nginx.conf $pkgdir/etc/nginx/

	find $pkgdir/etc -name "*.default" -delete
	rm -rf $pkgdir/var
	rm -rf $pkgdir/proc

	install -Dm0755 $srcdir/nginx $pkgdir/etc/service/nginx/run
	install -Dm0755 $srcdir/nginx-log $pkgdir/etc/service/nginx/log/run

	mkdir -p $pkgdir/var/log/nginx
	
	install -d $pkgdir/var/spool/nginx/body

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

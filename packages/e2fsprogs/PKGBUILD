# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=e2fsprogs
pkgver=1.44.1
pkgrel=1
pkgdesc="Standard Ext2/3/4 filesystem utilities"
arch=('x86_64' 'i686' 'aarch64' 'armv7l')
url="http://e2fsprogs.sourceforge.net"
license=('GPL')
groups=('base')
depends=('musl' 'util-linux')
backup=('etc/mke2fs.conf')
source=("https://downloads.sourceforge.net/project/$pkgname/$pkgname/v$pkgver/$pkgname-$pkgver.tar.gz")

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure \
		--prefix=/usr \
		--libdir=/usr/lib \
		--sbindir=/usr/bin \
		--sysconfdir=/etc \
		--build=$XHOST \
		--host=$XTARGET \
		--with-root-prefix="" \
		--enable-elf-shlibs \
		--disable-fsck \
		--disable-libblkid \
		--disable-libuuid \
		--disable-nls \
		--disable-tls \
		--disable-uuidd
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install install-libs

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}

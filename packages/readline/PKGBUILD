# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=readline
lbasever=8.0
patchlevel=0
stripped_lbasever="$(echo ${lbasever} | tr -d '.')"
pkgver=${lbasever}.${patchlevel}
pkgrel=1
pkgdesc="GNU readline library"
arch=('x86_64' 'x86' 'arm64' 'armv7l' 'armv5tel' 'ppc64le' 'ppc64' 'ppc')
url="http://tiswww.case.edu/php/chet/readline/rltop.html"
license=('GPL')
depends=('musl' 'ncurses')
backup=('etc/inputrc')
source=("https://ftp.gnu.org/gnu/readline/readline-$lbasever.tar.gz")
for patch in $(seq 1 ${patchlevel}); do
	source=(${source[@]} "http://ftp.gnu.org/gnu/readline/readline-${lbasever}-patches/readline${stripped_lbasever}-$(printf "%03d" ${patch})")
done
md5sums=('7e6c1f16aee3244a69aba6e438295ca3')

prepare() {
	cd "$srcdir"/$pkgname-$lbasever
	for patch in $(seq 1 ${patchlevel}); do
		patch -p0 -i "${srcdir}/readline${stripped_lbasever}-$(printf "%03d" ${patch})"
	done

	sed -i '/MV.*old/d' Makefile.in
	sed -i '/{OLDSUFF}/c:' support/shlib-install
	sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
	cd "$srcdir"/$pkgname-$lbasever
	./configure \
		--prefix='' \
		--build=$CHOST
	make SHLIB_LIBS="-L/lib -lncursesw"
}

package() {
	cd "$srcdir"/$pkgname-$lbasever
	make SHLIB_LIBS="-L/lib -lncursesw" DESTDIR="$pkgdir" install
}

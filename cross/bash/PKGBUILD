# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=bash
pkgver=4.4.18
pkgrel=3
pkgdesc="The GNU Bourne Again shell"
arch=('x86_64' 'aarch64' 'armhf' 'ppc64le' 's390x')
url="http://www.gnu.org/software/bash/bash.html"
license=('GPL')
groups=('base')
backup=(etc/bash.bash{rc,_logout} etc/skel/.bash{rc,_profile,_logout})
depends=('musl' 'readline')
provides=('sh')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz"
	"system.bashrc"
	"system.bash_logout"
	"dot.bashrc"
	"dot.bash_profile"
	"dot.bash_logout"
)

if [ "$NATIVE" = "no" ]
then
	export CROSSFLAGS="--build=$XHOST --host=$XTARGET"
else
	export CROSSFLAGS="--build=$($HOSTCC -dumpmachine)"
fi

build() {
	cd "$srcdir/$pkgname-$pkgver"

cat > config.cache << EOF
ac_cv_func_mmap_fixed_mapped=yes
ac_cv_func_strcoll_works=yes
ac_cv_func_working_mktime=yes
bash_cv_func_sigsetjmp=present
bash_cv_getcwd_malloc=yes
bash_cv_job_control_missing=present
bash_cv_printf_a_format=yes
bash_cv_sys_named_pipes=present
bash_cv_ulimit_maxfds=yes
bash_cv_under_sys_siglist=yes
bash_cv_unusable_rtsigs=no
gt_cv_int_divbyzero_sigfpe=yes
EOF

	_bashconfig=(-DDEFAULT_PATH_VALUE=\'\"/usr/local/sbin:/usr/local/bin:/usr/bin\"\'
			-DSTANDARD_UTILS_PATH=\'\"/usr/bin\"\'
			-DSYS_BASHRC=\'\"/etc/bash.bashrc\"\'
			-DSYS_BASH_LOGOUT=\'\"/etc/bash.bash_logout\"\'
			-DNON_INTERACTIVE_LOGIN_SHELLS)
	CFLAGS="$CFLAGS ${_bashconfig[@]}" \
	./configure $CROSSFLAGS \
		--prefix=/usr \
		--cache-file=config.cache \
		--without-bash-malloc \
		--with-installed-readline \
		--disable-nls
	make
}

package() {
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	ln -s bash $pkgdir/usr/bin/sh

	mkdir -p $pkgdir/etc/skel
	install -Dm644 $srcdir/system.bashrc $pkgdir/etc/bash.bashrc
	install -Dm644 $srcdir/system.bash_logout $pkgdir/etc/bash.bash_logout
	install -m644 $srcdir/dot.bashrc $pkgdir/etc/skel/.bashrc
	install -m644 $srcdir/dot.bash_profile $pkgdir/etc/skel/.bash_profile
	install -m644 $srcdir/dot.bash_logout $pkgdir/etc/skel/.bash_logout

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}
md5sums=('518e2c187cc11a17040f0915dddce54e'
         'd8f3f334e72c0e30032eae1a1229aef1'
         '472f536d7c9e8250dc4568ec4cfaf294'
         '027d6bd8f5f6a06b75bb7698cb478089'
         '2902e0fee7a9168f3a4fd2ccd60ff047'
         '42f4400ed2314bd7519c020d0187edc5')

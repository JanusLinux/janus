# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=binutils
pkgver=2.30
pkgrel=2
pkgdesc="A set of programs to assemble and manipulate binary and object files"
arch=('x86_64' 'aarch64' 'armhf' 's390x')
url="http://www.gnu.org/software/binutils/"
license=('GPL')
groups=('base')
depends=('musl' 'zlib')
source=("http://ftpmirror.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.xz")

if [ "$NATIVE" = "no" ]
then
	export CROSSFLAGS="--build=$XHOST --host=$XTARGET --target=$XTARGET"
else
	export CROSSFLAGS="--build=$($HOSTCC -dumpmachine)"
fi

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
	mkdir build
	cd build
	../configure $CROSSFLAGS \
		--prefix=/usr \
		--libdir=/usr/lib \
		--libexecdir=/usr/lib \
		--with-system-zlib \
		--with-pic \
		--enable-deterministic-archives \
		--enable-gold \
		--enable-ld=default \
		--enable-plugins \
		--enable-shared \
		--disable-multilib \
		--disable-nls \
		--disable-werror
	make configure-host
	make tooldir=/usr
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	cd build
	make tooldir=/usr DESTDIR=$pkgdir install

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}
md5sums=('ffc476dd46c96f932875d1b2e27e929f')

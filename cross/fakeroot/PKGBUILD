# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=fakeroot
pkgver=1.22
pkgrel=2
pkgdesc="Gives a fake root environment, useful for building packages as a non-privileged user"
arch=('x86_64' 'aarch64' 'armhf' 's390x')
url="http://fakeroot.alioth.debian.org/"
license=('GPL')
groups=('base')
depends=('musl' 'attr' 'acl' 'libcap' 'util-linux' 'sh' 'busybox')
source=("http://ftp.debian.org/debian/pool/main/f/fakeroot/fakeroot_$pkgver.orig.tar.bz2"
	'fakeroot-hide-dlsym-errors.patch'
	'fakeroot-no64.patch'
	'fakeroot-stdint.patch'
	'fakeroot-no-ldlibrarypath.patch'
	'xstatjunk.patch')

if [ "$NATIVE" = "no" ]
then
	export CROSSFLAGS="--build=$XHOST --host=$XTARGET"
else
	export CROSSFLAGS="--build=$($HOSTCC -dumpmachine)"
fi

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i $srcdir/fakeroot-hide-dlsym-errors.patch
	patch -Np1 -i $srcdir/fakeroot-no64.patch
	patch -Np1 -i $srcdir/fakeroot-stdint.patch
	patch -Np1 -i $srcdir/fakeroot-no-ldlibrarypath.patch
	patch -Np1 -i $srcdir/xstatjunk.patch
	for d in de es fr nl pt sv ; do
		sed -i '/man_MANS = /s@=.*@=@' "doc/${d}/Makefile.am"
	done
	sed -i 's@puts.*);\]@\]@g' configure.ac
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	export CFLAGS="-D_STAT_VER=0 $CFLAGS"
	CONFIG_SHELL=/bin/sh ./bootstrap
	CONFIG_SHELL=/bin/sh ./configure $CROSSFLAGS \
		--prefix=/usr
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}
md5sums=('fae64c9aeb2c895ead8e1b99bf50c631'
         '624cd86a66d020d01bde4ba60efc4d40'
         '3fc66a8ffb365fcf2acd652d33f101a2'
         '50a899380cfd5eae8ce3aaf878fb17b2'
         '75b41eae4e68552728c6df4cbce28bf2'
         '2438e71f447a784eac87f19a7fdbc531')

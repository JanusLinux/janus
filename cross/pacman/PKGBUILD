# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=pacman
pkgver=5.1.0
pkgrel=3
pkgdesc="A library-based package manager with dependency support"
arch=('x86_64' 'aarch64' 'armhf' 's390x')
url="http://www.archlinux.org/pacman/"
license=('GPL')
groups=('base')
depends=('musl' 'bash' 'curl' 'libarchive' 'fakeroot')
backup=('etc/pacman.conf'
	'etc/makepkg.conf')
source=("https://sources.archlinux.org/other/pacman/$pkgname-$pkgver.tar.gz"
        'pacman.conf'
	'makepkg.conf')

if [ "$NATIVE" = "no" ]
then
	export CROSSFLAGS="--build=$XHOST --host=$XTARGET"
else
	export CROSSFLAGS="--build=$($HOSTCC -dumpmachine)"
fi

build() {
	cd "$srcdir/$pkgname-$pkgver"
	sed -i -e '/x-cpio/s@)@|*application/x-empty*)@'  \
		-e 's/EUID == 0/EUID == -1/' \
		scripts/makepkg.sh.in

	autoreconf --force --install

	LDFLAGS="$LDFLAGS -static" \
	./configure $CROSSFLAGS \
		--prefix=/usr \
		--bindir=/usr/bin \
		--sysconfdir=/etc \
		--localstatedir=/var \
		--cache-file=config.cache \
		--with-scriptlet-shell=/usr/bin/bash \
		--without-gpgme \
		--disable-doc \
		--disable-nls \
		DUFLAGS="-sk" \
		SEDINPLACEFLAGS="-i"
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	install -dm755 $pkgdir/etc
	install -m644 $srcdir/pacman.conf $pkgdir/etc/pacman.conf
	install -m644 $srcdir/makepkg.conf $pkgdir/etc/makepkg.conf
	sed -i $pkgdir/etc/makepkg.conf \
		-e "s|@CARCH[@]|$BARCH|g" \
		-e "s|@CHOST[@]|$XTARGET|g" \
		-e "s|@CFLAGS[@]|$CFLAGS|g" \
		-e "s|@CPPFLAGS[@]|$CPPFLAGS|g" \
		-e "s|@LDFLAGS[@]|$LDFLAGS|g" \
		-e "s|@GCCOPTS[@]|$GCCOPTS|g"

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}
md5sums=('7b0460d449323eb3097ab39913d2668b'
         'e7ad3c4b105de22c4e6d76b78638aa93'
         'b6738e9bd282816b2b9606a5e8f07eaf')

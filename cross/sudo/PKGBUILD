# Maintainer: protonesso <nagakamira@gmail.com>

pkgname=sudo
pkgver=1.8.23
pkgrel=1
pkgdesc="Give certain users the ability to run some commands as root"
arch=('x86_64' 'aarch64' 'armhf' 's390x')
url="https://www.sudo.ws/sudo/"
license=('custom')
groups=('base')
depends=('musl' 'zlib')
backup=('etc/sudoers')
source=("https://www.sudo.ws/dist/$pkgname-$pkgver.tar.gz"
	'fix-cross-compile.patch'
	'fix-tests.patch'
	'libcrypt.patch'
	'musl-fix-headers.patch')
install=$pkgname.install

if [ "$NATIVE" = "no" ]
then
	export CROSSFLAGS="--build=$XHOST --host=$XTARGET"
else
	export CROSSFLAGS="--build=$($HOSTCC -dumpmachine)"
fi

prepare() {
	cd "$srcdir/$pkgname-$pkgver"
	patch -Np1 -i $srcdir/fix-cross-compile.patch
	patch -Np1 -i $srcdir/fix-tests.patch
	patch -Np1 -i $srcdir/libcrypt.patch
	patch -Np1 -i $srcdir/musl-fix-headers.patch
}

build() {
	cd "$srcdir/$pkgname-$pkgver"
	./configure $CROSSFLAGS \
		--prefix=/usr \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib \
		--with-rundir=/run/sudo \
		--with-vardir=/var/lib/sudo \
		--with-all-insults \
		--with-env-editor \
		--without-pam \
		--enable-pie \
		--disable-nls
	make
}

package() {
	unset MAKEFLAGS
	cd "$srcdir/$pkgname-$pkgver"
	make DESTDIR=$pkgdir install

	msg "Stripping package $pkgname..."
	find $pkgdir -type f | xargs file 2>/dev/null | grep "LSB executable"       | cut -f 1 -d : | xargs $STRIP --strip-all			2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "shared object"        | cut -f 1 -d : | xargs $STRIP --strip-unneeded		2>/dev/null || true
	find $pkgdir -type f | xargs file 2>/dev/null | grep "current ar archive"   | cut -f 1 -d : | xargs $STRIP --strip-debug		2>/dev/null || true
	cd $pkgdir
	rm -rf usr/{,local/}{,share/}{doc,man,info,locale} usr{,/local}{,/share},opt/*}/{man,info} usr/{,local/}{,share/}{doc,gtk-doc} opt/*/{doc,gtk-doc}
	rm -rf $pkgdir/{,usr/}lib/charset.alias
}
md5sums=('ea444d747feb1decfebdffd0b38b0739'
         '9c27fb2304054854798cfed02e8ad1a0'
         '660d93c7ae0416e985d6c8f6344c5985'
         '048e1cc360537bcea5b74a874fd41674'
         '9017a82095a79bea78be29f0dd23122a')
